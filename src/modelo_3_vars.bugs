model{
	for(i in 1:n.celdas){
		frec.1[i] ~ dpois(exp(beta[i]))
		beta[i] <- beta.0 + beta.s[sexo[i]] + beta.c[ciudad[i]] +
			beta.e[eval.imagen[i]] +
			beta.sc[sexo[i], ciudad[i]] +
			beta.se[sexo[i], eval.imagen[i]] +
			beta.ce[ciudad[i], eval.imagen[i]] 
		sim.n[i] ~ dpois(exp(beta[i]))
	}
		
	beta.0 ~ dnorm(0,0.0001)
	
	for(j in 1:2){
		beta.c[j] ~ dnorm(0, c.tau)
	}	
	c.tau <- pow(c.sigma, -2)
	c.sigma ~ dunif(0.001,1000)
	
	for(j in 1:2){
		beta.s[j] ~ dnorm(0, s.tau)
	}	
	s.tau <- pow(s.sigma, -2)
	s.sigma ~ dunif(0.001,1000)


	for(j in 1:3){
		beta.e[j] ~ dnorm(0, e.tau)
	}	
	e.tau <- pow(e.sigma, -2)
	e.sigma ~ dunif(0.001,1000)

	for(j in 1:2){
		for(k in 1:2){
			beta.sc[j,k] ~ dnorm(0, sc.tau)
		}
	}
	sc.tau <- pow(sc.sigma, -2)
	sc.sigma ~ dunif(0.01,100)

	for(j in 1:2){
		for(k in 1:3){
			beta.se[j,k] ~ dnorm(0, se.tau)
		}
	}
	se.tau <- pow(se.sigma, -2)
	se.sigma ~ dunif(0.01,100)

	for(j in 1:2){
		for(k in 1:3){
			beta.ce[j,k] ~ dnorm(0, ce.tau)
		}
	}
	ce.tau <- pow(ce.sigma, -2)
	ce.sigma ~ dunif(0.01,100)

	for (i in 1:2){
		for(j in 1:2){
			for(k in 1:3){
				beta.sce[i,j,k] ~ dnorm(0, sce.tau)
			}
		}
	}
	sce.tau <- pow(sce.sigma, -2)
	sce.sigma ~ dunif(0.01,100)

}